plugins {
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
    id "com.jfrog.artifactory" version "4.20.0"
}

ext {
    kotlin_version = '1.3.50'
    spock_version = '1.3-groovy-2.4'
    groovy_version = '2.4.5'
    fuel_version = '2.2.1'
    wiremock_version = '2.25.1'
    gson_version = '2.8.5'
    slf4j_version = '1.7.28'
    logback_version = '1.2.3'
}

project.group = 'com.kryszak'
project.version = '1.2'

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.groovy.srcDirs += 'src/test/groovy'
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

artifactory {
    publish {
        contextUrl = 'https://kryszak.jfrog.io/artifactory'
        repository {
            repoKey = 'gwatlin'
            username = System.getenv("ARTIFACTORY_USER")
            password = System.getenv("ARTIFACTORY_PASSWORD")
        }
        defaults {
            publications('mavenJava')
            publishConfigs('archives', 'published')
            publishBuildInfo = false
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "com.github.kittinunf.fuel:fuel:$fuel_version"
    implementation "com.github.kittinunf.fuel:fuel-gson:$fuel_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "ch.qos.logback:logback-core:$logback_version"
    testImplementation "org.spockframework:spock-core:$spock_version"
    testImplementation "org.codehaus.groovy:groovy-all:$groovy_version"
    testImplementation "com.github.tomakehurst:wiremock-jre8:$wiremock_version"
}

jar.doLast { task ->
    ant.checksum file: task.archivePath
}
