plugins {
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.8.20"
}

ext {
    kotlin_version = '1.8.20'
    spock_version = '1.3-groovy-2.5'
    groovy_version = '2.5.22'
    fuel_version = '2.3.1'
    wiremock_version = '2.27.2'
    gson_version = '2.10.1'
    slf4j_version = '2.0.7'
    logback_version = '1.4.6'
}

project.group = 'com.kryszak'
project.version = '1.3'

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.groovy.srcDirs += 'src/test/groovy'
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java
        }
  }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/kryszak/gwatlin"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
  }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.github.kittinunf.fuel:fuel:$fuel_version"
    implementation "com.github.kittinunf.fuel:fuel-gson:$fuel_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "ch.qos.logback:logback-core:$logback_version"
    testImplementation "org.spockframework:spock-core:$spock_version"
    testImplementation "org.codehaus.groovy:groovy-all:$groovy_version"
    testImplementation "com.github.tomakehurst:wiremock:$wiremock_version"
}

jar.doLast { task ->
    ant.checksum file: task.archivePath
}
