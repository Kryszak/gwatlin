buildscript {
    ext {
        kotlin_version = '1.3.50'
        spock_version = '1.3-groovy-2.4'
        groovy_version = '2.4.5'
        fuel_version = '2.2.1'
        wiremock_version = '2.25.1'
        gson_version = '2.8.5'
        slf4j_version = '1.7.28'
        logback_version = '1.2.3'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'kotlin'
apply plugin: "groovy"
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

project.group = 'com.kryszak'
project.version = '1.0'


sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.groovy.srcDirs += 'src/test/groovy'
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = "kryszak"
    key = System.getenv("BINTRAY_KEY")
    publications = ['mavenJava'] //When uploading Maven-based publication files
    dryRun = false
    publish = true
    pkg {
        repo = "gw2"
        name = 'gwatlin'
        desc = 'Guild Wars 2 API client written in Kotlin'
        websiteUrl = 'https://github.com/Kryszak/gwatlin'
        issueTrackerUrl = 'https://github.com/Kryszak/gwatlin/issues'
        vcsUrl = 'https://github.com/Kryszak/gwatlin.git'
        licenses = ['MIT']
        publicDownloadNumbers = true
        version {
            name = "v${project.version}"
            released = new Date()
            vcsTag = "v${project.version}"
            mavenCentralSync {
                sync = false //Optional (true by default). Determines whether to sync the version to Maven Central.
            }
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "com.github.kittinunf.fuel:fuel:$fuel_version"
    compile "com.github.kittinunf.fuel:fuel-gson:$fuel_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.google.code.gson:gson:$gson_version"
    compile "org.slf4j:slf4j-api:$slf4j_version"
    compile "ch.qos.logback:logback-classic:$logback_version"
    compile "ch.qos.logback:logback-core:$logback_version"
    testCompile "org.spockframework:spock-core:$spock_version"
    testCompile "org.codehaus.groovy:groovy-all:$groovy_version"
    testCompile "com.github.tomakehurst:wiremock-jre8:$wiremock_version"
}

jar.doLast { task ->
    ant.checksum file: task.archivePath
}
